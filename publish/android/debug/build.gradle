apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "ringrtc-android-debug"
version          = version_number
group            = group_info

android {

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    sourceSets {
        main {
            jniLibs.srcDirs = debug_jni_lib_dirs
        }
    }

    defaultConfig {
        consumerProguardFiles proguard_file
    }

    android.libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName =  "${archivesBaseName}-${version}.aar"
        }
    }
}

dependencies {
    for (String dir : debug_jni_lib_dirs) {
        implementation fileTree(dir: dir, include: ['*.jar'])
    }
    testImplementation 'junit:junit:4.12'
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: releaseRepoUrl) {
            authentication(userName: releaseRepoUsername, password: releaseRepoPassword)
        }

        pom.project {
            name 'ringrtc-android'
            packaging 'aar'
            description 'RingRTC for Android'
            url 'https://github.com/signalapp/ringrtc/'

	    scm {
	      url 'https://github.com/signalapp/ringrtc/'
	      connection 'scm:git:git@github.com:signalapp/ringrtc.git'
	      developerConnection 'scm:git:git@github.com:signalapp/ringrtc.git'
	    }

            licenses {
                license {
                    name 'GPL-3.0-only'
                    url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Curt Brune'
                    organizationUrl 'https://www.signal.org/'
                }
                developer {
                    name 'Jim Gustafson'
                    organizationUrl 'https://www.signal.org/'
                }
            }
        }
    }
}

task installArchives(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository url: "file://${System.properties['user.home']}/.m2/repository"
        }
    }
}

